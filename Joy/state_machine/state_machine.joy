"../meta_joy/meta.joy" include.
DEFINE 

(* 
A state is a 2 items list, where the first item is the value, 
the second is a transition function.
The state transition function takes a pair of stack and current value, and produces a new stack and state:
[stack value] -> new-stack new-state
*)

state-value == 0 at;
next-state == 1 at;

# moves the first element from one list into a list pair with the second argument: [x rest] y -> [rest] [y x]
move-first == swap 
	[null] [swap [] cons] 
	[[rest] [first] cleave swapd [] cons cons] 
	ifte;

(* 
Checks whether the state run is done, this is true if the input is done.
input stack state -> boolean
*)
finish-state-run == pop pop null;

# runs a state against an input list: input stack state -> final-state-value
run-state-from-stack == [finish-state-run] [popd popd state-value] [
	[move-first] dip
	next-state i # input [stack value] -> input new-stack new-state
] tailrec;

# runs a state from an input list and an empty stack: input state -> final state value
run-state == [] swap run-state-from-stack;

# manipulation of [stack value] pairs
cur-stack == 0 at;
input == 1 at;

# applies a function to the top of the stack and returns a new stack: [stack value] func -> new-stack
on-stack  == swap cur-stack uncons [swap i] dip cons;

# same as cond, but without the default value
condn == [[]] concat cond;

# runs the state of a baby, initializing the mistakes count to 0: input state -> final state value
run-baby-state == [0] swap run-state-from-stack;

# increments/decrement the value on the top of the stack
one-up == [succ] on-stack;
one-down == [pred] on-stack;

# the maximal number of failures a parent is allowed to have
max-fail == 3;
  
call-social-worker == ["call-social-worker" [[
  [[input "sing-lullaby" =] cur-stack call-social-worker]
  [[input "feed" =] cur-stack call-social-worker]
  [[input "soothe" =] cur-stack call-social-worker]
] condn]];

  
sleepy == ["sleepy" [[
  [[input "sing-lullaby" =] one-down asleep]
  [[input "feed" =] one-up crying]
  [[input "soothe" =] one-up crying]
] condn]];
  
hungry == ["hungry" [[
  [[input "sing-lullaby" =] one-up crying]
  [[input "feed" =] one-down asleep]
  [[input "soothe" =] one-up crying]
] condn]];
  
asleep == ["asleep" [[
  [[input "sing-lullaby" =] one-up crying]
  [[input "feed" =] one-up crying]
  [[input "soothe" =] one-up crying]
] condn]];
  
crying == ["crying" [[
  [[input "sing-lullaby" =] [cur-stack first max-fail >=] [cur-stack call-social-worker] [one-up crying] ifte]
  [[input "feed" =] [cur-stack first max-fail >=] [cur-stack call-social-worker] [one-up crying] ifte]
  [[input "soothe" =] one-down asleep]
] condn]];

.