(*
Code for meta-programming in Joy. 
Usage examples in insert_at.joy.
Note: code using the functions below may get GC heavy, so it won't work well on the Joy
interpreter without the BDW GC.
*)
DEFINE
	# maps a function over the items of an arbitrarily nested list: list func -> list
	treemap == [map] treerec;
	
	# flattenes an arbitrarily nested list into a flat list: list -> list
	treeflatten  ==  [] swap [swons] treestep reverse;
	
	# appending an item to the end of a list: list val -> list
	append == swap [] enconcat;
	
	# flattens a list of lists into a single list: [list] -> list
	flatten == [] [
		[list]
		[concat]
		[append]
		ifte] fold;
		
	# converts an item into a singleton list: val -> [val]
	to-list == [] cons;
	
	(*
		Applies a function to each leaf of a tree and concatenates the results: tree func -> tree.
		Where func is: val -> list.
		For example:
		foo == dup [] cons cons;
		[1 [2 [3 4]] 3] [foo] treemap-concat # => [1 1 [2 2 [3 3 4 4]] 3 3]
	*)
	treemap-concat == [map flatten to-list] treerec first;
	
	# fetches the second item of a list: list -> val
	second == 1 at;
	
	# wraps two items into a list: a b -> [a b]
	tuple == to-list cons;
	
	# checks whether a list or string starts with a given value: list val -> boolean
	starts-with == swap first =;
	
	# the character used as a prefix for splice patterns
	splice-char == '~;
	
	# checks whether a symbol is a splice pattern, i.e. its string form starts with '~: symbol -> boolean
	is-splice-pattern == name splice-char starts-with;
	
	# converts a splicing pattern into a string: symbol -> string
	splice-to-str == name rest;
	
	(* splicing from a list *)
	
	# converts a string to number: string -> num
	to-num == 0 strtol;
	
	# converts a number to a string: num -> string
	num-to-str == 'i 1 1 format;
	
	# checks whether a string represents a number: string -> boolean
	is-num-string == dup to-num  num-to-str =;
	
	# converts a splicing pattern into a number: symbol -> num
	splice-to-num == name rest to-num;
	
	# converts a number into a splice pattern: num -> ~num
	num-to-splice == num-to-str splice-char swap cons;
	
	# checkes whether a symbol is a numeric splicing pattern: symbol -> boolean
	is-num-splice-pattern == 
		[is-splice-pattern]
		[splice-to-str is-num-string]
		[false] ifte;
		
	# fetches all numeric splice patterns from a tree: tree -> list
	num-splice-patterns == treeflatten [is-num-splice-pattern] filter;
	
	# applies num-splice-patterns to a tree and converts them into numbers
	splice-patterns-nums == num-splice-patterns [splice-to-num] map;
	
	(*
		Replacing all occurrences of the pattern "~X" (where X is a number) in 
		an arbitrarily nested list with the X numbered items of a list: list program -> program
		So this:
		["four" "two" "five"] [~0  [1 ~1 3] [~2 6 7] enconcat] splice-from-list.
		becomes :
		["four"  [2 "two" 4] ["five" 6 7] enconcat] 
	*)
	splice-from-list == [ 
		[is-num-splice-pattern] 
		[splice-to-num at] 
		[]
		ifte
	] treemap popd;
	
	# takes a specified number of items from the top of the stack and converts them to a list: items num -> list
	take-from-stack == [] swap [cons] times;

	(* 
		The same as splice-from-list, but takes a perdefined number of items from the stack 
		as the list to be spliced in:  program num -> program
		So this code is equivalent to the example in splice-from-list:
		"four" "two" "five"  [~0  [1 ~1 3] [~2 6 7] enconcat] 3 splice-from-stack.
	*)
	splice-from-stack ==  swap [take-from-stack] dip splice-from-list;
		
	# finds the numeric value of the maximal splicing pattren in the tree (-1 for no splice patterns): tree -> num
	max-splice-num == splice-patterns-nums -1 [max] fold;
	
	(*
		The same as splice-from-stack with the numeric argument inferred from the maximal value of the splicing
		pattern: program -> program.
		So this code is equivalent to the example in splice-from-stack:
		"four" "two" "five"  [~0  [1 ~1 3] [~2 6 7] enconcat] splice-from-stack-max.
	*)
	splice-from-stack-max == [max-splice-num 1 +] nullary splice-from-stack;
	
	# the same as splice-from-stack-max but evaluates the resulting program
	splice == splice-from-stack-max i;
	
	(* splicing from a map *)
	
	# converts a map entry into a string key: [key value] -> string key
	get-key == first name;
	
	# gets the value frome a key value entry; [key value] -> [value]
	get-value == rest;
	
	# creates a predicate to test equality with a map entry's key: key -> predicate
	key-predicate == [get-key] [=] enconcat;
	
	(* 
		Takes a map and a symbol key and tries to find the occurence of the key in the map: map key -> value.
		The map has the following structure: [[key1 value1] [key2 value2] ...]
		Where each value can be anything and and the key is a Joy word converted to a string using "name".
		The function assumes that the key is present in the map.
	*)
	find-by-key == 
		name
		key-predicate filter 
		first get-value;	
		
	# checks whether a symbol key is present in a map: map key -> boolean.
	key-in-map == name key-predicate some;
	
	(* The same splice-from-map before application of the resulting program: map program -> program. *)
	splice-from-map-no-eval == [ 
		[key-in-map] 
		[find-by-key] 
		[]
		ifte
	] treemap-concat popd;
	
	(*
		Replacing all occurrences of the pattern "X" (where X is a symbol) in 
		an arbitrarily nested list with the X keyed items of a map, then applying
		the resulting list as a program: map program -> result-of-program
		So this:
		[[four "four"] [two "two"] [five "f" "ive" concat]] [four  [1 two 3] [five 6 7] enconcat] splice-from-map.
		becomes (before application):
		["four" [1 "two" 3] ["f" "ive" concat 6 7] enconcat]
		which results in:
		[1 "two" 3 "four" "f" "ive" concat 6 7]
	*)
	splice-from-map == splice-from-map-no-eval i;
	
	(*
		The sake as splice-from-map with the input map and program joined into a single argument: [map program] -> result-of-program.
		Note that the program should not be quoted.
		So this code is equivalent to the example in splice-from-map:
		[[[four "four"] 
		  [two "two"] 
		  [five "f" "ive" concat]] 
		  
		 four  [1 two 3] [five 6 7] enconcat
		 ] let-map.
	*)
	let-map ==  [first] [rest] cleave splice-from-map;
	
	(*
		Same as let-map, but optionally allows stack splicing patterns as in splice-from-stack-max: [map program] -> result-of-program.
		So this code is equivalent to the example in let:
		"four" "two"
		[[[four ~0] 
		  [five "f" "ive" concat]] 
		  
		 four  [1 ~1 3] [five 6 7] enconcat
		 ] let.
	*)
	let == splice-from-stack-max let-map;
	
	(*
		Converts a list into a map, where each item in the list is mapped to an indexed splice pattern: list -> map.
		Example: [a b c] list-to-splice # => [[c ~2] [b ~1] [a ~0]]
	*)
	list-to-splice == 
		[[] 0] 
		[[
			[[new-list ~0 first] [index ~0 second] [cur ~1]] 
			[cur] index num-to-splice intern append
			new-list cons 
			index succ tuple
		] let] 
		fold first;
	
	(*
		Like let but infers the symbol values from the stack: [list program] -> result-of-program.
		So this:
		"four" "five" "two" 
		[[four five] 
		  
		 four  [1 ~2 3] [five 6 7] enconcat
		] let-splice.
		 
		Becomes:
		[1 "two" 3 "four" "five" 6 7]
	*)
	let-splice == [first list-to-splice] [rest] cleave cons let;
.